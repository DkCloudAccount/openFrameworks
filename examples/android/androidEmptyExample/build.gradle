final ofRoot = '../../../'
final APP_ID = "cc.openframeworks.androidEmptyExample"
final addons = ofRoot + 'addons'

// Load common functions
apply from: ofRoot+"libs/openFrameworksCompiled/project/android/common-functions.gradle"



// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle-experimental:0.7.0-alpha4'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 22
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId = APP_ID
            minSdkVersion.apiLevel    = 9
            targetSdkVersion.apiLevel = 22
            versionCode     =  1
            versionName     = "1.0"
        }
    }

    android.ndk {
        moduleName = ofAppModuleName()
        toolchain  = buildToolchain()
        stl        = compilerStl()

        cppFlags.addAll(cppFlags(ofRoot))
        ldLibs.addAll(ldLibs(ofRoot))
        ldFlags.addAll(ldCompilerFlags())
    }

    android.sources {
        main {
            manifest.source {
                srcDir '.'
                include 'AndroidManifest.xml'
            }
            jni {
                source {
                    srcDirs= ['src',
                              ofRoot+"/addons/ofxAndroid/src",
                              ofRoot+"/addons/ofxAccelerometer/src"
                    ]
                    excludes = []
                }

                // Link to openFrameworks
                dependencies {
                    project ":openFrameworksProject"  linkage "static"
                }
            }

            java.source.srcDirs = ['srcJava']
            resources.source.srcDirs = ['srcJava']
            aidl.source.srcDirs = ['srcJava']
            renderscript.source.srcDirs = ['srcJava']
            res.source.srcDirs = ['res']
            assets.source.srcDirs = ['assets']
            jniLibs.source.srcDirs = ['libs']
        }
    }

    android.lintOptions {
        abortOnError  = false
    }

    android.buildTypes {
        release {
            minifyEnabled = false
        }
    }

    // Setup the different types of flavors (arm / x86),
    // and add linker flags based on that
    android.productFlavors {
        getAbis().each { abi ->
            create(getFlavorName(abi)) {
                ndk {
                    abiFilters.add(abi)
                    getPrebuiltLibPaths(abi, ofRoot).each { path ->
                        ldFlags.add( path.toString())
                    }

                }
            }
        }
    }
}


dependencies {
    compile project(path: ':ofAndroidLib')
}


